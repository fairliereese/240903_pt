
import pandas as pd
import os
import sys
import pyranges as pr

p = os.path.dirname(os.path.dirname(os.getcwd()))+'/scripts/'
sys.path.append(p)

from sm_utils import *
from utils import *
# from vcf_utils import *

c_dir = '../common/'

meta_file = '../config.tsv'
configfile: '../config.yml'

include: f'{c_dir}download.smk'
include: f'{c_dir}samtools.smk'
include: f'{c_dir}winnowmap.smk'
include: f'{c_dir}bigwig.smk'
include: f'{c_dir}variant_calling.smk'
include: f'{c_dir}formatting.smk'
include: f'{c_dir}phasing.smk'
include: f'{c_dir}cerberus.smk'
include: f'{c_dir}bedtools.smk'
include: f'{c_dir}transdecoder.smk'
include: f'{c_dir}protein.smk'
include: f'{c_dir}minimap2.smk'
include: f'{c_dir}stats.smk'
include: f'{c_dir}lr-kallisto.smk'
include: f'{c_dir}kallisto.smk'
include: f'{c_dir}suppa.smk'

# sample information
meta_file = '../config.tsv'

df = load_meta()
df = df.loc[~df['sample'].str.contains('_')]
df['lab_sample'] = df['lab_number_sample'].astype(str)+'_'+\
                      df['lab_sampleid'].astype(str)+'_'+\
                      df['cell_line_id'].astype(str)
df = df.loc[df.mixed_samples==False]
df = df[['cell_line_id', 'sample', 'hapmap_DNA_ID', 'lab_sample']].drop_duplicates()


def get_df_val(df, col1, col_dict):
    temp = df.copy(deep=True)

    for key, item in col_dict.items():
        temp = temp.loc[temp[key] == item]

    val = temp[col1].unique()
    assert len(val) == 1
    return val[0]

rule all:
    input:
        config['ref']['ccre']['pls_bed'],
        config['lr']['lapa']['odir'],
        config['lr']['lapa']['config'],
        expand(config['lr']['lapa']['pls_int'],
               sample=df['sample'].tolist())

rule lapa_config:
    input:
        files = expand(config['lr']['q10_bam'],
               lab_sample=df['lab_sample'].tolist())
    params:
        samples = df['lab_sample'].tolist(),
        df = df
    resources:
        nodes = 1,
        threads = 1
    output:
        config = config['lr']['lapa']['config']
    run:
        samples_2 = []
        for f,s in zip(input.files, params.samples):
            assert s in f
            samples_2 += [get_df_val(params.df, 'sample', {'lab_sample':s})]

        df = pd.DataFrame()
        df['sample'] = samples_2
        df['dataset'] = samples_2
        df['path'] = list(input.files)
        df.to_csv(output.config, sep=',', index=False)

use rule fa_get_chr_lens as hg38_get_chr_lens with:
    input:
        fa = config['ref']['fa']
    output:
        chr_lens = config['ref']['fa_chr_lens']

rule gtf_utr_fix:
    input:
        gtf = config['ref']['gtf']
    resources:
        threads = 1,
        nodes = 2
    output:
        gtf = config['ref']['gtf_utr_fix']
    conda:
        'lapa'
    shell:
        """
        gencode_utr_fix \
            --input_gtf {input.gtf} \
            --output_gtf {output.gtf}
        """

rule lapa_tss:
    input:
        config = config['lr']['lapa']['config'],
        fa = config['ref']['fa'],
        annot = config['ref']['gtf_utr_fix'],
        chrom_sizes = config['ref']['fa_chr_lens']
    resources:
        nodes = 3,
        threads = 16
    output:
        directory(config['lr']['lapa']['odir']),
        sample_beds = expand(config['lr']['lapa']['sample_tss'],
                             sample=df['sample'].tolist())
    conda:
        "lapa"
    shell:
        """
        lapa_tss --alignment {input.config} \
        	--fasta {input.fa} \
        	--annotation {input.annot} \
        	--chrom_sizes {input.chrom_sizes} \
        	--output_dir {output}
        """


############## ccres
use rule wget as dl_ccre_pls with:
    params:
        link = config['ref']['ccre']['pls_link']
    output:
        out = config['ref']['ccre']['pls_bed']

############## ccres intersction
rule int_pls_tss_bed:
    input:
        pls = config['ref']['ccre']['pls_bed'],
        tss = config['lr']['lapa']['sample_tss']
    resources:
        threads = 8,
        nodes = 1
    output:
        tsv = config['lr']['lapa']['pls_int']
    run:
        pls = pr.read_bed(input.pls)
        tss = pr.read_bed(input.tss)

        # deduplicate -- arises from assingment of
        # several genes to the same tss
        tss = tss.df
        tss.rename({'ItemRGB': 'tpm'}, axis=1, inplace=True)
        tss['rid'] = tss['Chromosome'].astype(str)+'_'+\
                     tss['Start'].astype(str)+'_'+\
                     tss['End'].astype(str)
        tss = tss[['Chromosome', 'Start', 'End', 'tpm', 'rid']]
        tss = tss.drop_duplicates()
        l1 = len(tss.index)
        assert len(tss) == len(tss.rid.unique())

        # merge w/ pls
        tss = pr.PyRanges(tss)
        tss = tss.join(pls,
                       strandedness=None,
                       how='left')

        # deduplicate entries from original TSSs by whether the intersect
        # at least one ccre
        tss = tss.df
        tss['int_pls'] = False
        tss.loc[tss.Start_b != -1, 'int_pls'] = True
        gb_cols = ['Chromosome', 'Start', 'End', 'Strand', 'tpm', 'rid']
        keep_cols = gb_cols+['int_pls']
        tss = tss[keep_cols].groupby(gb_cols, observed=True).max().reset_index()

        # make sure there are no dupes
        assert len(tss) == len(tss.rid.unique())
        l2 = len(tss.index)
        assert l1 == l2

        tss.to_csv(output.tsv, sep='\t', index=False)
